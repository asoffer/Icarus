#{export}
if ::= scope {
  enter ::= jump(b: bool) { goto b, then(), else() | done() }

  then ::= block {
    before ::= () -> () {}
    after ::= jump() { goto done() }
  }

  else ::= block {
    before ::= () -> () {}
    after ::= jump() { goto done() }
  }

  exit ::= () -> () {}
}

#{export}
while ::= scope {
  enter ::= jump(b: bool) { goto b, do(), done() }

  do ::= block {
    before ::= () -> () {}
    after ::= jump() { goto start() }
  }

  exit ::= () -> () {}
}

#{export}
repeat ::= scope (int64) {
  enter ::= jump [state: *int64] (n: int64) {
    @state = n
    goto @state == 0, done(), do() 
  }

  do ::= block {
    before ::= () -> () {}
    after ::= jump [state: *int64] () { 
      @state -= 1
      goto @state == 0, done(), do()
    }
  }

  exit ::= () -> () {}
}

// TODO: We shouldn't need to export any of this.
#{export}
range ::= struct {
  #{export}
  current: int64
  #{export}
  end: int64
}

#{export}
for ::= scope (range) {
  enter ::= jump [state: *range] (begin: int64, end: int64) {
    @state = range.{ current = begin \\ end = end }
    goto state.current >= state.end, done(), do(state.current)
  }

  do ::= block {
    before ::= (n: int64) => n
    after ::= jump [state: *range] () { 
      state.current += 1 
      goto state.current >= state.end, done(), do(state.current)
    }
  }

  exit ::= () -> () {}
}
