core   ::= import "core.ic"
errno  ::= import "errno.ic"
cstdio ::= import "cstdio.ic"
memory ::= import "memory.ic"

#{export}
File ::= struct {
  #{export}
  make ::= (f: *cstdio.File) -> File {
    return File.{ _handle = f }
  }

  #{export} _handle: *cstdio.File
}

#{export}
CFileHandle ::= (f: File) => f._handle

#{export}
With ::= scope(File) {
  enter ::= jump [f: *File] (name: []char) {
    f._handle = cstdio.fopen(name.data, "r".data)
    e := errno.get()
    goto e == errno.error.SUCCESS, open(@f), error(e) | done()
  }

  open ::= block {
    before ::= (f: File) => f
    after ::= jump [f: *File] () {
      cstdio.fclose(f._handle)
      goto done()
    }
  }

  error ::= block {
    before ::= (e: errno.error) => e
    after ::= jump [f: *File] () { goto done() }
  }

  exit ::= () -> () {}
}

#{export}
read_to ::= (f: File, T :: type) -> T {
  result: T
  CHUNK_SIZE ::= 1024 as u64
  num_read := CHUNK_SIZE
  buffer: [CHUNK_SIZE; char]
  core.while (num_read == CHUNK_SIZE) do {
    num_read = cstdio.fread(&buffer[0], char'bytes, CHUNK_SIZE, f._handle)
    result'append(slice(&buffer[0], num_read))
  }
  return result
}

LinesState ::= struct {
  handle: *cstdio.File
  line: [*]char
  bufsize: u64
  line_length: i64

  (destroy) ::= (self: *LinesState) -> () {
    memory.deallocate(char, self.line)
  }
}

next_line ::= (s: *LinesState) -> bool {
  s.line_length = cstdio.getline(&s.line, &s.bufsize, s.handle)
  return s.line_length > 0
}

as_slice ::= (s: *LinesState) -> []char {
  return slice(s.line, s.line_length as u64)
}

#{export}
Lines ::= scope(LinesState) {
  enter ::= jump [state: *LinesState] (f: File) {
    state.handle = f'CFileHandle
    goto state'next_line(), each(state'as_slice()), done()
  }

  each ::= block {
    before ::= (line: []char) => line
    after ::= jump [state: *LinesState] () {
      goto state'next_line(), each(state'as_slice()), done()
    }
  }

  exit ::= () -> () {}
}
