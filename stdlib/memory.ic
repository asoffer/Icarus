core ::= import "core.ic"

c       ::= import "c/types.ic"
cstr    ::= import "c/string.ic"
cstdlib ::= import "c/stdlib.ic"

#{export}
allocate ::= (T :: type) -> *T {
  return cstdlib.malloc(builtin.bytes(T)) as *T
}

#{export}
allocate ::= (T :: type, l: u64) -> []T {
  return builtin.slice(cstdlib.malloc(builtin.bytes(T) * l) as [*]T, l)
}

#{export}
// TODO: Accept either a pointer or a buffer pointer.
deallocate ::= (ptr: ~`T) -> () {
  cstdlib.free(ptr as [*]byte)
}

#{export}
reverse_destroy_buffer ::= (ptr: ~[*]`T, count: u64) -> () {
  p := ptr + count
  core.repeat (count as i64) do {
    p -= 1
    destroy p
  }
}

#{export}
clone ::= (m: []byte) => expand(m, m.length)

#{export}
expand ::= (m: []byte, new_size: u64) -> []byte {
  buffer := allocate(byte, new_size)
  cstr.memcpy(buffer.data, m.data, m.length)
  return buffer
}

#{export}
fill ::= (m: []byte, value: u8) -> () {
  cstr.memset(m.data, value as c.int, m.length)
}

#{export}
Copy ::= (from: []byte, to: []byte) -> () {
  cstr.memcpy(to.data, from.data, to.length)
}

// TODO: If we don't require length to be a multiple of alignment, we need to account for that here.
#{export}
to_raw ::= (s: ~[]`T) => builtin.slice(s.data as [*]byte, s.length * builtin.bytes(T))
