core   ::= import "core.ic"
memory ::= import "memory.ic"

// TODO: The precise types here are actually architecture dependent. We need to
// provide some platform-querying capabilities so that we don't need to, for
// example hard-code malloc's parameter to u64 when in reality it is
// `std::size_t` which may be 32-bits on some platforms.


// TODO: This exists inside the core module, but for some reason we calling it
// is causing compiler crashes.
exchange ::= (x: *~`T, y: T) -> T {
  result: T
  result = @x
  @x = y
  return result
}

#{export}
string ::= struct {
  _data:     [*]char
  _length:   u64
  _capacity: u64

  (move) ::= (from: *string) -> string {
    return string.{
      _data     = exchange(from._data, null)
      _length   = exchange(from._length, 0)
      _capacity = exchange(from._capacity, 0)
    }
  }

  (move) ::= (to: *string, from: *string) -> () {
    (to._data,     from._data)     = (from._data,     null)
    (to._length,   from._length)   = (from._length,   0)
    (to._capacity, from._capacity) = (from._capacity, 0)
  }

  (copy) ::= (from: *string) -> string {
    return string.{
      _data     = memory.clone(from'raw_null_terminated_view).data
      _length   = from._length
      _capacity = from._capacity
    }
  }

  (copy) ::= (to: *string, from: *string) -> () {
    memory.deallocate(to._data)
    to._length   = from._length
    to._capacity = from._capacity
    to._data     = memory.clone(to'raw_null_terminated_view).data
  }

  (destroy) ::= (self: *string) -> () { memory.deallocate(self._data) }
}

// Resizes the buffer, copying the string data to the new buffer, excluding the
// null terminator. Assumes `new_capacity` is at least as large as`s._capacity`.
resize_buffer ::= (s: *string, new_capacity: u64) -> () {
  buffer := memory.expand(s'raw_null_terminated_view, new_capacity + 1)
  memory.deallocate(s._data)
  s._data = buffer.data
  s._capacity = new_capacity
}

// TODO: These functions should be defined inside `string`
#{export}
make ::= (fill: char, len: u64) -> string {
  buffer := memory.allocate(char, len + 1)
  memory.fill(buffer, fill as u8)
  buffer[len] = init !\0
  return string.{
    _data     = buffer.data
    _length   = len
    _capacity = len
  }
}

#{export}
make ::= (s: []char) -> string {
  raw := s'memory.to_raw
  raw = slice(raw.data, raw.length + 1)
  return string.{
    _data     = memory.clone(raw).data
    _length   = s.length
    _capacity = s.length
  }
}

#{export}
make ::= (capacity: u64) -> string {
  buffer := memory.allocate(char, capacity + 1)
  buffer[0] = init !\0
  return string.{
    _data     = buffer.data
    _length   = 0
    _capacity = capacity
  }
}


#{export}
length ::= (s: *string) => s._length

#{export}
is_empty ::= (s: *string) => s._length == 0

#{export}
append ::= (s: *string, character: char) -> () {
  core.if (s._capacity == 0) then {
    resize_buffer(s, 1)
  } else core.if (s._capacity == s._length) then {
    resize_buffer(s, s._capacity * 2)
  }
  s._data[s._length] = init character
  s._length += 1
  s._data[s._length] = init !\0
}

#{export}
append ::= (s: *string, char_slice: []char) -> () {
  core.if (s._length + char_slice.length > s._capacity) then {
    resize_buffer(s, s._length + char_slice.length)
  }

  core.if (char_slice.length > 0) then {
    len := char_slice.length + 1
    memory.Copy(from = slice(char_slice.data, len),
                to   = slice(s'at(s._length), len))
    s._length += char_slice.length
  }
}

#{export}
append ::= (s: *string, from: *string) -> () {
  s'append(from'view)
}

#{export}
Print ::= (s: *string) -> () {
  printf ::= foreign("printf", ([*]char, [*]char) -> i32)
  core.if (not s'is_empty) then { printf("%s".data, s._data) }
}

#{export}
at ::= (s: *string, n: u64) => &s._data[n]

// TODO: The call to `at` here should be allowed because it couldn't possibly match itself.
#{export}
at ::= (s: *string, lo: u64, hi: u64) => slice(&s._data[lo], hi - lo)

#{export}
view ::= (s: *string) => slice(s'at(0), s._length)

raw_null_terminated_view ::= (s: *string) => slice(s'at(0), s._length + 1)
